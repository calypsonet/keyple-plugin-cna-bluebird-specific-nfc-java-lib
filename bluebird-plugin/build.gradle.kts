import java.time.Year
plugins {
    id("com.android.library")
    id("kotlin-android")
    id("kotlin-parcelize")
    id("org.jetbrains.dokka")
    id("com.diffplug.spotless")
    id("maven-publish")
}

val title: String by project
val copyright: String by project
val currentYear = Year.now().value.toString()

android {
    namespace = "org.calypsonet.keyple.plugin.bluebird"
    compileSdk = 35

    buildFeatures {
        viewBinding = true
    }

    defaultConfig {
        minSdk = 21
    }

    buildTypes {
        getByName("release") {
            isMinifyEnabled = false
            proguardFiles(
                getDefaultProguardFile("proguard-android-optimize.txt"),
                "proguard-rules.pro"
            )
        }
    }

    val javaSourceLevel: String by project
    val javaTargetLevel: String by project
    compileOptions {
        sourceCompatibility = JavaVersion.toVersion(javaSourceLevel)
        targetCompatibility = JavaVersion.toVersion(javaTargetLevel)
    }

    kotlinOptions {
        jvmTarget = javaTargetLevel
    }

    lint {
        abortOnError = false
    }

    sourceSets {
        getByName("main").java.srcDirs("src/main/kotlin")
        getByName("debug").java.srcDirs("src/debug/kotlin")
        getByName("test").java.srcDirs("src/test/kotlin")
        getByName("androidTest").java.srcDirs("src/androidTest/kotlin")
    }
}

dependencies {
    // Kotlin
    implementation(kotlin("stdlib-jdk8"))
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:1.9.0")

    // Bluebird libs
    compileOnly(fileTree(mapOf("dir" to "libs", "include" to listOf("*.jar"))))

    // Begin Keyple configuration (generated by 'https://keyple.org/components/overview/configuration-wizard/')
    implementation("org.eclipse.keyple:keyple-common-java-api:2.0.1")
    implementation("org.eclipse.keyple:keyple-plugin-java-api:2.3.1")
    implementation("org.eclipse.keyple:keyple-util-java-lib:2.4.0")
    // End Keyple configuration

    // Storage card support
    implementation("org.eclipse.keyple:keyple-plugin-storage-card-java-api:1.0.0-SNAPSHOT") { isChanging = true}

    // Logging
    implementation("com.jakewharton.timber:timber:5.0.1")
}

tasks {
    dokka {
        moduleName.set(rootProject.name)
        dokkaSourceSets.main {
            includes.from("src/main/kdoc/overview.md")
            sourceLink {
                localDirectory.set(file("src/main/kotlin"))
                remoteUrl("https://github.com/calypsonet/${rootProject.name}")
                remoteLineSuffix.set("#L")
            }
        }
        pluginsConfiguration.html {
            footerMessage.set(copyright.replace("YEAR", currentYear))
        }
    }
}

val dokkaJar = tasks.register<Jar>("dokkaJar") {
    dependsOn(tasks.dokkaGenerate)
    from(tasks.dokkaGeneratePublicationHtml.flatMap { it.outputDirectory })
    archiveClassifier.set("html-docs")
}

publishing {
    publications {
        // This says: defer this block until all of the other stuff has run first.
        // This is required since components["release"] is generated by the Android
        // plugin in `afterEvaluate` itself, which forces us to do the same.
        afterEvaluate {
            // Create a new publication called "release". The maven-publish plugin
            // creates tasks named publish${name}PublicationTo${target}, where
            // ${name} is a capitalized form of the name and ${target} is an output
            // repository. By default a MavenLocal target is automatically added,
            // which outputs to ~/.m2/repository.
            create<MavenPublication>("mavenJava") {
                // Include all artifacts from the "release" component. This is the
                // .aar file along with the sources and javadoc .jars.
                from(components["release"])
                // add Dokka to the publication
                artifact(tasks.named("dokkaJar"))
                // Here we configure some properties of the publication (these are
                // automatically applied to the pom file). Your library will be
                // referenced as ${groupId}:${artifactId}.
                groupId = project.group.toString()
                artifactId = rootProject.name
                version = project.version.toString()
                pom {
                    name.set(title)
                    description.set(project.description)
                    url.set("https://github.com/eclipse/${rootProject.name}")
                    organization {
                        name.set("Calypso Networks Association")
                        url.set("https://calypsonet.org/")
                    }
                    licenses {
                        license {
                            name.set("Eclipse Public License - v 2.0")
                            url.set("http://www.eclipse.org/legal/epl-2.0")
                        }
                    }
                    developers {
                        developer {
                            name.set("Calypso Networks Association Technical Team")
                            email.set("support-dev@calypsonet.org")
                        }
                    }
                    scm {
                        connection.set("scm:git:git://github.com/eclipse/${rootProject.name}.git")
                        developerConnection.set("scm:git:ssh://github.com/eclipse/${rootProject.name}.git")
                        url.set("https://github.com/eclipse/${rootProject.name}")
                    }
                }
            }
        }
    }
}

tasks {
    dokka {
        moduleName.set(rootProject.name)
        dokkaSourceSets.main {
            includes.from("src/main/kdoc/overview.md")
            sourceLink {
                localDirectory.set(file("src/main/kotlin"))
                remoteUrl("https://github.com/calypsonet/${rootProject.name}")
                remoteLineSuffix.set("#L")
            }
        }
        pluginsConfiguration.html {
            footerMessage.set(copyright.replace("YEAR", currentYear))
        }
    }
}


